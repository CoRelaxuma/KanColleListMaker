/*
* SoftWareName(Jp)	: 艦これ一覧めいかー
* SoftWareName(En)	: KanColleListMaker
* Version			: 1.2
* Author			: kanahiron
* License			: MIT
* Copyright(C) 2014 kanahiron
*/

#uselib "shlwapi.dll"
#cfunc PathIsDirectory "PathIsDirectoryA" sptr

#uselib "shell32.dll"
#func DragAcceptFiles "DragAcceptFiles" int,int
#func DragQueryFile   "DragQueryFileA"  int,int,int,int
#func DragQueryPoint  "DragQueryPoint"  int,int
#func DragFinish      "DragFinish"      int

#uselib "gdi32.dll"
#cfunc CreateDC "CreateDCA" sptr,sptr,sptr,int
#func DeleteDC "DeleteDC"  int
#func BitBlt "BitBlt" int,int,int,int,int,int,int,int,int

#uselib "user32.dll"
#func global MoveWindow "MoveWindow" int , int , int , int , int , int
#func GetWindowLong "GetWindowLongA" int,int
#func SetWindowLong "SetWindowLongA" int,int,int
#func SetLayeredWindowAttributes "SetLayeredWindowAttributes" int,sptr,int,int
#func GetCursorPos "GetCursorPos" int
#func SetCursorPos "SetCursorPos" int,int
#func ScreenToClient "ScreenToClient" int ,int
#func ChildWindowFromPoint "ChildWindowFromPoint" int ,int
#func SetFocus "SetFocus" int
#func SetParent "SetParent" int ,int
#func RegisterHotKey "RegisterHotKey" int,int,int,int
#func UnregisterHotKey "UnregisterHotKey" int,int
#cfunc IsWindowVisible "IsWindowVisible" int
#cfunc GetSystemMetrics "GetSystemMetrics" int
#func SetClassLong "SetClassLongA" int,int,int
#func LoadCursor "LoadCursorA" int,int
#func SetTimer "SetTimer" int,int,int,sptr
#func KillTimer "KillTimer" int,int

#uselib "winmm.dll"
#cfunc  timeGetTime "timeGetTime"

#include "kmodule.as"
#include "ImageFileModule2.as"
#include "inimodule.as"
#include "exdialog.as"
#include "makelistmodule_Ver5_9.as"

#pack "data.png"
#packopt name "艦これ一覧めいかー Ver1_2"
#packopt hide 1

#define HOTKEYID	7789
#define timerID		100
#define ctype round(%1) int(strf("%%0.0f", %1))

	onexit gosub *en_d

	mode = 0
	listmode = 0
	yubiwa = 1
	kantai = 1
	autosearch = 1
	hidename = 0
	sti = 0
	cliflag = 0
	cellmode = 0
	mesy = 30
	hotkeycap = 0
	ret = 0
	enablemultidisplay = 1
		
	jpgquality = 90
	namecut = 0
	hidelog = 0
	jpgsave = 0
	rapidshooting = 0
	frontrow = 0
	othergame = 0
	
	availablecap = 0
	availableyabumi = 0
	hotkeyset = 0
	
	tempint = 0
	tempstr = ""
	
	dim memberlistxy,2
	dim data,8,5,2
	dim bc,2
	dim dd,4,2
	dim mesxy,2
	dim maxscrwh,2
	dim mindexxy,2
	dim mxy,2
	dim mxy_,2
	dim dmxy,2
	dim sscap,2
	dim sscapwh,2
	dim cellnum,2,2
	dim erasecell,2,2
	dim scrsize,2,2
	dim cellsize,2,2
	dim tempmouse,2
	dim disinfo,4
	dim ecdd,4,2
	ddim Aspectratio,2
	sdim sscapmes,256
	sdim logmessage,256
	sdim modemes,64,4
	sdim stemp
	sdim sssavedir ,260
	sdim listsavedir ,260
	sdim yabumidir,260
	sdim savekind,64,2
	sdim hotkeycapchar
	sdim hotkeystr
	sdim hotkeystr_temp
	sdim savelogtext,1024
	cellnum(0,0) = 6,4
	cellnum(0,1) = 4,2
	erasecell(0,0) = 6,4
	erasecell(0,1) = 4,2
	AspectRatio(0) = 0.5081, 0.47895
	dd(0,0) = 387,154,389,279
	dd(0,1) = 330,100,455,365
	ecdd(0,0) = 0,38,194,138
	ecdd(0,1) = 0,176,228,182
	bc(0) = 20,200
	maxscrwh(0) = ginfo(20),ginfo(21)
	mindexxy(0) = 0,0
	modemes(0) = "SSキャプチャ","艦娘一覧","攻略編成","オプション"
	mxy(0) = 0,0
	mxy_(0) = 0,0
	sscapwh = 0,0
	seccapf = 0
	sscapmes = "取得する"
	savekind = ".png",".jpg"
	seqcapmes = "開始"
	scrsize(0,0) = limit(1358,210,round(0.66*maxscrwh(0))),round(AspectRatio(0)*scrsize(0,0))
	scrsize(0,1) = limit(1120,250,round(0.66*maxscrwh(0))),round(AspectRatio(1)*scrsize(0,1))
	;dialog ""+scrsize(0,1)+"  "+scrsize(1,1)
	cellsize(0,0) = round(1.0*scrsize(0,0) / (cellnum(0,0)+1)) ,round(1.0*scrsize(1,0) / (cellnum(1,0)+1))
	cellsize(0,1) = round(1.0*scrsize(0,1) / (cellnum(0,1)+1)) ,round(1.0*scrsize(1,1) / (cellnum(1,1)+1))
	
	gosub *iniload
	gosub *inisave
	gosub *disinfoinit
	
	//全体キャプチャ保存用
	buffer 10,disinfo(2),disinfo(3)
	//手動選択重ね用
	bgscr 11,disinfo(2),disinfo(3),2
	hwnd11 = hwnd
	font "メイリオ",30

	//範囲選択用レイヤードウィンドウ
	bgscr 9,ginfo(20),ginfo(21),2
	hwnd9 = hwnd
	color 0,255,0
	boxf
	GetWindowLong hwnd9,-20
	SetWindowLong hwnd9,-20,stat|$80000
	SetLayeredWindowAttributes hwnd9,0x00000000,128,LWA_ALPHA

	//
	screen 1,maxscrwh(0),maxscrwh(1),2:title "表示させたいセルにD&D"
	hwnd1 = hwnd
	DragAcceptFiles hwnd1, 0
	oncmd gosub *OnDropFiles, WM_DROPFILES
	oncmd gosub *windowsize, WM_SIZING
	onclick gosub *listdrag
	framesx = ginfo_sizex - ginfo_winx
	framesy = ginfo_sizey - ginfo_winy
	
	
	//一覧モード出力用
	buffer 2,disinfo(2),disinfo(3)

	//画像読み込み用
	buffer 7
	picload "data.png"
	
	
	;//キャプチャ作成用
	buffer 8
	//キャプ用
	buffer 5,disinfo(2),disinfo(3)

	//背景コピー用
	buffer 4,maxscrwh(0),maxscrwh(1)
	//縮小処理用
	buffer 3,maxscrwh(0)/5,maxscrwh(1)/3
	
	screen 0,350,500:title "メイン"
	hwnd0 = hwnd
	DragAcceptFiles hwnd0, 0
	oncmd gosub *OnDropFiles ,WM_DROPFILES
	oncmd gosub *setmode ,WM_COMMAND
	oncmd gosub *hotkey ,WM_HOTKEY
	oncmd gosub *sscaptcha,0x0113
	
	color 240,240,240:boxf:color
	font "メイリオ",13
	objmode 2
	
	objsize 175,30
	pos 0,0
	combox cellmode,30,"  SSキャプチャモード\n  艦娘一覧モード\n  攻略編成モード\n  オプション"
	comboxid = stat
	hcombox = objinfo_hwnd(comboxid)
	objsize 0,0
	button "dummy",*dummy
	
	gosub *ssmode
	gosub *hotkeycapsetting

	if autosearch {	
		await
		gosub *sssetting
	}

	stop

*dummy
	
*setmode
	oncmd 0
	if (lparam = hcombox) & (((wparam>>16) &0x0000FFFF) = 1){
	
		if mode = 1 | mode = 2{
			DragAcceptFiles hwnd1, 0
			DragAcceptFiles hwnd0, 0
			gsel 1,0
			onclick 0
		}
	
		if mode = 3{
			gosub *inisave				
			gosub *hotkeycapsetting
			gosub *disinfoinit
		}
	
		sendmsg hCombox, $147
		mode = stat
	
		if mode = 1 | mode = 2{
			DragAcceptFiles hwnd1, 1
			DragAcceptFiles hwnd0, 1
		}
	
		if mode = 0{
			gosub *ssmode
		}
		if mode = 1{
			listmode = 0
			gsel 1,0
			onclick 1
			gosub *list
		}
		if mode = 2{
			listmode = 1
			gsel 1,0
			onclick 1
			gosub *list
		}
		if mode = 3{
			gosub *option
		}
	}
	oncmd 1
return

//リストモード――――――――――――――――――――――――――――――――――
*list
	
	mxy(0) = 0,0
	mxy_(0) = 0,0
	cliflag = 0
	mesy = 28

	gsel 0,2
	clrobj 1
	color 240,240,240:boxf:color

	if listmode = 0{	
		width 175,168-(70*hidelog)
		
		objsize 175,20
		pos 5,mesy
		chkbox "指輪も範囲に入れる",yubiwa:mesy+=20
		chkbox "所属艦隊も範囲に入れる",kantai:mesy+=20
	}
	if listmode = 1{
		width 175,128-(70*hidelog)

	}
			
	objsize 150-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),30
	pos 0,mesy
	button gosub "作成",*make
	
	objsize 25,30
	pos 150-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),mesy
	button gosub "開",*listsavefopen
	fopenbid = stat
	
	if ((yabumiautoupload = 1) & (availableyabumi = 1)) {
		pos 150,mesy
		button gosub "UP",*yabumiupload
		yabumibid = stat
	}

	
	mesy+=30

	
	pos 0,mesy
	mesbox logmessage,175,70,0
	logid = stat
	
	if IsWindowVisible(hwnd1) = 0{
		gsel 1,1
	} else {
		gsel 1,0
	}

	title modemes(listmode+1)+"モード - 表示させたいセルにD&D"
	width scrsize(0,listmode),scrsize(1,listmode)
	
	gosub *draw
	
return

*listdrag
	
	if ginfo(3) != 1:return	
	onclick 0

	repeat

		stick sti,256
		if sti = 256{
	
			mxy(0) = mousex/cellsize(0,listmode) ,mousey/cellsize(1,listmode)
	
			if mxy(0) = erasecell(0,listmode) & mxy(1) = erasecell(1,listmode) & cliflag = 0{
				
				cliflag = 1
				mxy_(0) = mxy(0),mxy(1)
				mindexxy(0) = (mousex-(mxy_(0)*(cellsize(0,listmode)-1))),(mousey-(mxy_(1)*(cellsize(1,listmode)-1)))
				
				gsel 4
				pos 0,0
				gcopy 1,0,0,maxscrwh(0),maxscrwh(1)
	
				gsel 3
				pos 0,0
				gzoom cellsize(0,listmode),cellsize(1,listmode),7,ecdd(0,listmode),ecdd(1,listmode),ecdd(2,listmode),ecdd(3,listmode),1

				gsel 1,0
			}
			
			if (data(mxy(0),mxy(1),listmode)) >= 10 & cliflag = 0{
				
				cliflag = 1
				mxy_(0) = mxy(0),mxy(1)
				mindexxy(0) = (mousex-(mxy_(0)*(cellsize(0,listmode)-1))),(mousey-(mxy_(1)*(cellsize(1,listmode)-1)))
				
				gsel 4
				pos 0,0
				gcopy 1,0,0,maxscrwh(0),maxscrwh(1)
	
				gsel 3
				pos 0,0
				gzoom cellsize(0,listmode),cellsize(1,listmode),data(mxy_(0),mxy_(1),listmode),dd(0,listmode),dd(1,listmode),dd(2,listmode),dd(3,listmode),1

				gsel 1,0
			}
		
		}
	
		if cliflag = 1{
			redraw 0
			pos 0,0
			gcopy 4,0,0,maxscrwh(0),maxscrwh(1)
			gmode 3,cellsize(0,listmode),cellsize(1,listmode),100
			pos mousex-mindexxy(0),mousey-mindexxy(1)
			gcopy 3,0,0,cellsize(0,listmode),cellsize(1,listmode)
			gmode 0
			redraw 1
		}
		
		if sti = 0 & cliflag = 1{
	
			if mxy_(0) = erasecell(0,listmode) & mxy_(1) = erasecell(1,listmode) {
				erasecell(0,listmode) = mxy(0)
				erasecell(1,listmode) = mxy(1)
				data(mxy(0),mxy(1),listmode) = 0
			}

			if mxy(0) = erasecell(0,listmode) & mxy(1) = erasecell(1,listmode) {
				data(mxy_(0),mxy_(1),listmode) = 0
			} else {
				temp = data(mxy_(0),mxy_(1),listmode) 
				data(mxy_(0),mxy_(1),listmode) = data(mxy(0),mxy(1),listmode)
				data(mxy(0),mxy(1),listmode) = temp
			}
	
			mxy(0) = 0,0
			mxy_(0) = 0,0
			temp = 0
			cliflag = 0
			
			gosub *draw
		}
	
		if sti = 0 & cliflag = 0{
			break
		}

		await 16

	loop
	onclick 1
	
return

//SSモード――――――――――――――――――――――――――――――――――
*ssmode
	
	gsel 1,-1
	gsel 0,1+frontrow
	width 175,213-(70*hidelog)
	clrobj 1
	color 240,240,240:boxf:color

	mesy = 31
	
	pos 2,mesy
	mes "キャプチャ座標"
		
	objsize 80,25
	pos 95,mesy-3
	button gosub sscapmes,*sssetting
	sscapiid = stat
	mesy+=21
		
	objsize 150-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),30
	pos 0,mesy
	button gosub "SSをキャプチャする",*sscaptcha
	sscapbid = stat

	objsize 25,30
	pos 150-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),mesy
	button gosub "開",*sssavefopen
	fopenbid = stat
	
	if ((yabumiautoupload = 1) & (availableyabumi = 1)) {
		pos 150,mesy
		button gosub "UP",*yabumiupload
		yabumibid = stat
	}
	
	mesy+=29
	pos 2,mesy+2
	mes "連続キャプチャ"
	objsize 80,25
	pos 95,mesy
	button gosub seqcapmes,*seqcap
	seqcapbid = stat
	
	mesy+=24
		
	objsize 175,20
	pos 5,mesy
	chkbox "座標自動検索",autosearch:mesy+=20
	pos 5,mesy
	chkbox "提督名と司令部Lvを隠す",hidename:mesy+=20

	pos 0,mesy
	mesbox logmessage,175,70,0
	logid = stat

	if availablecap = 0{
		objenable sscapbid,0
		objenable seqcapbid,0
		if (yabumiautoupload = 1) & (availableyabumi = 1){
			objenable yabumibid,0
		}
	} else {
		objenable sscapbid,1
		objenable seqcapbid,1
		if (yabumiautoupload = 1) & (availableyabumi = 1){
			objenable yabumibid,1
		}
	}
	
return
	
//オプション――――――――――――――――――――――――――――――――――
*option

	gsel 1,-1
	gsel 0,1+frontrow
	width 350,405
	clrobj 1
	color 240,240,240:boxf:color
	
	mesy = 30
	pos 4,mesy
	mes "readmeをよくお読み下さい":mesy+=20
	
	objsize 230,20
	pos 5,mesy
	chkbox "提督名を隠すのではなくカットする",namecut:mesy+=20
	pos 5,mesy
	chkbox "常にメインウィンドウを最前列表示",frontrow:mesy+=20
	pos 5,mesy
	chkbox "ログを非表示",hidelog:mesy+=20
	pos 5,mesy
	chkbox "yabumiに自動でアップロードする",yabumiautoupload:mesy+=20
	pos 5,mesy
	chkbox "艦これ以外も手動で取得可能にする",othergame:mesy+=20
	pos 5,mesy
	chkbox "ホットキーでSSをキャプチャする",hotkeycap:mesy+=20

	objsize 50,20
	pos 30,mesy
	chkbox "Alt",hotkeycapalt
	pos 80,mesy
	chkbox "Ctrl",hotkeycapctrl
	pos 130,mesy
	chkbox "Shift",hotkeycapshift
	pos 180,mesy
	chkbox "Win",hotkeycapwin
	pos 240,mesy
	input hotkeycapchar,20,20,1:mesy+=20
	
	objsize 230,20
	pos 5,mesy
	chkbox "＊一覧モードでSSを自動追加する",autoaddcaptcha:mesy+=20

	objsize 150,20
	pos 5,mesy
	chkbox "jpgで保存する",jpgsave:mesy+=20
	
	pos 5,mesy
	mes "jpg保存品質"
	pos 155,mesy
	mes "1〜100(大きいほど高画質)"
	pos 100,mesy
	input jpgquality,50,20,3:mesy+=20
	
	pos 5,mesy
	mes "連続キャプチャの間隔"
	pos 205,mesy
	mes "x0.1秒 (0.1〜99.9秒)"
	pos 150,mesy
	input timersec,50,20,3:mesy+=20

	objsize 150,20
	
	pos 14,mesy
	mes "一覧保存先"
	pos 200,mesy
	button gosub "一覧保存先変更",*listsavedialog:mesy+=20
	pos 0,mesy
	mesbox listsavedir,350,45,5,259:mesy+=45
	listsdid = stat
	
	pos 14,mesy
	mes "SS保存先"
	pos 200,mesy
	button gosub "SS保存先変更",*sssavedialog:mesy+=20
	pos 0,mesy
	mesbox sssavedir,350,45,5,259:mesy+=45
	sssdid = stat
	
	pos 14,mesy
	mes "YabumiUploader.exeの場所"
	pos 200,mesy
	button gosub "変更",*yabumiuploaderdialog:mesy+=20
	pos 0,mesy
	mesbox yabumidir,350,45,5,259:mesy+=45
	yabumiid = stat

return


//一覧作成――――――――――――――――――――――――――――――――――
*make
	
	yoko = 0
	tate = 0
	capx = 0
	capy = 0
	w = 0
	h = 0
	
	repeat cellnum(1,listmode)+1
		ycnt = cnt
		repeat cellnum(0,listmode)+1
			if data(cnt,ycnt,listmode) >= 20{
				if yoko <= cnt:yoko = cnt
				if tate <= ycnt:tate = ycnt
			}
		loop
	loop
	yoko++
	tate++
	
	if listmode = 0{
		if yubiwa = 0 & kantai = 0 : w = 377 : capx = 398
		if yubiwa = 1 & kantai = 1 : w = 435 : capx = 363
		if yubiwa = 1 & kantai = 0 : w = 400 : capx = 398
		if yubiwa = 0 & kantai = 1 : w = 412 : capx = 363
		h = 279
		capy = 154
	} else {
		w = 455
		h = 365
		capx = 330
		capy = 100
	}

	buffer 2,w*yoko,h*tate

	drawcount = 0
	repeat cellnum(1,listmode)+1
		ycnt = cnt
		repeat cellnum(0,listmode)+1
			if data(cnt,ycnt,listmode) >= 10{
				pos cnt*w,ycnt*h
				gsel data(cnt,ycnt,listmode)
				if ginfo(13) = 450:cutf = 1
				gsel 2
				gcopy data(cnt,ycnt,listmode),capx,capy-(cutf*30),w,h 
				drawcount++
				cutf = 0
			}
		loop
	loop

	savename = ""
	if drawcount != 0{
		repeat
			savename = strf("%s_%03d%s",modemes(listmode+1),cnt,savekind(jpgsave))
			if kmexist(listsavedir +"\\"+ savename) = -1{
				gdiimagesave listsavedir +"\\"+ savename,jpgquality
				break
			}
		loop
		
		gsel 0,2
			
		if kmexist(listsavedir +"\\"+ savename) <= 0{
			logmessage = savename+"の保存に失敗しました"
			objprm logid,logmessage
		} else {
			logmessage = savename+"を保存しました"
			objprm logid,logmessage
		}
	
	} else {
		gsel 0,2
		logmessage = "画像が一枚もありません"
		objprm logid,logmessage
	}

	
	dim mxy,2
	dim mxy_,2
	dim data,8,5,2
	dim bc,2
	dim dmxy,2
	dim data,8,5,2
	bc(0) = 20,200
	
	gosub *draw
	
return

//ドロップ処理――――――――――――――――――――――――――――――――――
*OnDropFiles

	hdrop = wParam
	sdim dropfile,260
	DragQueryFile hdrop, 0, varptr(dropfile), 260
	DragFinish hdrop
	if peek(dropfile,0) = '"' : getstr dropfile, dropfile, 1, '"'
	
	if ImgF_GetFormat(dropfile) = 0{
		dropfile = 0
		gsel 1
		return
	}
	
	addfilename = dropfile
	gosub *addcaptcha
	
return

*addcaptcha
	
	if (ginfo(24) = 0) | (autoaddcaptchaf = 1) {
		autoaddcaptchaf = 0
		
		dmxy(0) = cellnum(0,listmode),cellnum(1,listmode)
		
		repeat (cellnum(1,listmode)+1)*(cellnum(0,listmode)+1)
			ycnt = cnt\(cellnum(1,listmode)+1)
			xcnt = cnt/(cellnum(1,listmode)+1)
			if data(xcnt,ycnt,listmode) = 0{
				dmxy(0) = xcnt, ycnt
				break
			}
		loop
	
	} else {
		dmxy(0) = mousex/cellsize(0,listmode) , mousey/cellsize(1,listmode)
	}

	bc(listmode)++
	data(dmxy(0),dmxy(1),listmode) = bc(listmode)
	buffer bc(listmode)
	ImgF_PicloadEx addfilename
	gsel 1
	gosub *draw
	
	gsel 0
	logmessage = strf("%sをセル(%d,%d)に読み込みました",getpath(addfilename,11),dmxy(0),dmxy(1))
	objprm logid,logmessage
	
	gsel 1

	dmxy(0) = 0,0
	addfilename = ""
	
return

//描画――――――――――――――――――――――――――――――――――
*draw
	
	gsel 1,0
	redraw 0
	color 255,255,255
	boxf
	color
	
	repeat cellnum(0,listmode),1
		line  cnt*cellsize(0,listmode),-1,cnt*cellsize(0,listmode),maxscrwh(1)+1
	loop
	repeat cellnum(1,listmode),1
		line  -1,cnt*cellsize(1,listmode),maxscrwh(0)+1,cnt*cellsize(1,listmode)
	loop
	
	pos erasecell(0,listmode)*cellsize(0,listmode) ,erasecell(1,listmode)*cellsize(1,listmode)
	gzoom cellsize(0,listmode),cellsize(1,listmode),7,ecdd(0,listmode),ecdd(1,listmode),ecdd(2,listmode),ecdd(3,listmode),1

	repeat cellnum(1,listmode)+1
		ycnt = cnt
		repeat cellnum(0,listmode)+1
			if data(cnt,ycnt,listmode) >= 10{
				pos cnt*cellsize(0,listmode),ycnt*cellsize(1,listmode)
				gzoom cellsize(0,listmode),cellsize(1,listmode),data(cnt,ycnt,listmode),dd(0,listmode),dd(1,listmode),dd(2,listmode),dd(3,listmode),1
			}
		loop
	loop
	redraw 1
return

//SSキャプチャセッティング――――――――――――――――――――――――――――――――――
*sssetting
	
	;gsel 0,-1
	;wait 40
	gsel 10
	redraw 0
	hdcScreen = CreateDC("DISPLAY", NULL, NULL, NULL)
	BitBlt hdc,0,0, disinfo(2), disinfo(3), hdcScreen,  disinfo(0), disinfo(1),SRCCOPY
	DeleteDC hdcScreen
	redraw 1

	if autosearch = 0 {
		gsel 11,-1
		pos 0,0
		gcopy 10,0,0,disinfo(2),disinfo(3)
		color 255,255,255
		boxf abs(disinfo(0)),abs(disinfo(1)),abs(disinfo(0))+970,abs(disinfo(1))+45
		color
		pos abs(disinfo(0))+5,abs(disinfo(1))+2
		mes "艦これの画面を覆うようにドラッグしてください Escキーでキャンセル"
		gsel 11,2
		MoveWindow hwnd11,disinfo(0),disinfo(1),disinfo(2),disinfo(3),1
		LoadCursor 0, 32515
		SetClassLong hwnd, -12, stat
		getkancollewindowposmanual 10,sscap,9
		gsel 11,-1
		LoadCursor 0, 32512
		SetClassLong hwnd, -12, stat
	} else {
		
		getkancollewindowposauto 10,sscap
	}
	
	sscapwh(0) = sscap(2)-sscap(0),sscap(3)-sscap(1)

	gsel 0,1+frontrow
	
	if ( (sscapwh(0) = 800) & (sscapwh(1) = 480) ) | ( (othergame=1) & (autosearch=0) ){
		sscapmes = strf("%d,%d",sscap(0),sscap(1))
		objprm sscapiid,sscapmes
		objenable sscapbid,1
		objenable seqcapbid,1
		logmessage = "座標の取得に成功しました"
		objprm logid,logmessage
		availablecap = 1
	
		if (yabumiautoupload = 1) & (availableyabumi = 1){
			objenable yabumibid,1
		}
		
	} else {
		sscapmes = "取得する"
		objprm sscapiid,sscapmes
		objenable sscapbid,0
		objenable seqcapbid,0
		logmessage = "座標を取得できません\n詳細はreadmeをお読みください"
		objprm logid,logmessage
		availablecap = 0
		if (yabumiautoupload = 1) & (availableyabumi = 1){
			objenable yabumibid,0
		}
	}

return

//SSキャプチャ――――――――――――――――――――――――――――――――――
*sscaptcha
	oncmd 0
	
	buffer 5,sscapwh(0),sscapwh(1)
	redraw 0
	hdcScreen = CreateDC("DISPLAY", NULL, NULL, NULL)
	BitBlt hdc, 0, 0, sscapwh(0), sscapwh(1), hdcScreen, sscap(0), sscap(1),SRCCOPY
	DeleteDC hdcScreen
	redraw 1

	if (homeport(5) = 1) & (hidename = 1) & (sscapwh(0) = 800) & (sscapwh(1) = 480) {

		pos 113,0
		gcopy 7,0,0,120,22
		pos 395,10
		gcopy 7,0,22,100,16

		if (namecut = 1) {
			buffer 5,800,450
			redraw 0
			hdcScreen = CreateDC("DISPLAY", NULL, NULL, NULL)
			BitBlt hdc, 0, 0, 800, 480,  hdcScreen, sscap(0), sscap(1)+30,SRCCOPY
			DeleteDC hdcScreen
			redraw 1
		}
	
	}
	
	savename = strf("kancolle_%04d%02d%02d-%02d%02d%02d%03d%s",gettime(0),gettime(1),gettime(3),gettime(4),gettime(5),gettime(6),gettime(7),savekind(jpgsave))
	gdiimagesave sssavedir +"\\"+ savename,jpgquality
	
	gsel 0
		
	if kmexist(sssavedir +"\\"+ savename) <= 0{
		logmessage = savename+"の保存に失敗しました"
		objprm logid,logmessage
	} else {
		logmessage = savename+"を保存しました"
		objprm logid,logmessage
	}
 oncmd 1
return

*seqcap
	
	if seqcapf = 0{
		SetTimer hwnd0,timerID,100*timersec,0
		seqcapmes = "停止"
		objprm seqcapbid,seqcapmes
		objenable comboxid,0
		objenable sscapbid,0
		objenable sscapiid,0
		if (yabumiautoupload = 1) & (availableyabumi = 1){
			objenable yabumibid,0
		}
		seqcapf = 1
		gosub *sscaptcha
	} else {
		KillTimer hwnd0,timerID
		seqcapmes = "開始"
		objprm seqcapbid,seqcapmes
		objenable comboxid,1
		objenable sscapbid,1
		objenable sscapiid,1
		if (yabumiautoupload = 1) & (availableyabumi = 1){
			objenable yabumibid,1
		}
		seqcapf = 0
	}
	
return

*yabumiupload
	
	if stat = yabumibid {	
		gosub *sscaptcha
		exec yabumidir+" \""+(sssavedir +"\\"+ savename)+"\""
	}
	if stat = yabumilistbid{
		gosub *make
		if savename != ""{
			exec yabumidir+" \""+(listsavedir +"\\"+ savename)+"\""
		}
	}
return

//ホットキー――――――――――――――――――――――――――――――――――
*hotkey
	oncmd 0
	
	if (wparam = HOTKEYID) & (availablecap = 1){

		gosub *sscaptcha
		if (mode = 1 | mode = 2) {

			if autoaddcaptcha{
				addfilename = sssavedir +"\\"+ savename
				gosub *addcaptcha
			}
		}
		await 16
	}

	oncmd 1
return

*windowsize
	oncmd 0

	dupptr left,   lparam,    4
	dupptr top,    lparam+4,  4
	dupptr right,  lparam+8,  4
	dupptr bottom, lparam+12, 4

	switch wparam
	case 1
	case 2
	case 7
	case 8
		bottom = top + round(AspectRatio(listmode) * (right - left - framesx)) + framesy
		swbreak
	case 3
	case 5
	case 6
		right = left + round(1.0/AspectRatio(listmode) * (bottom - top - framesy)) + framesx
		swbreak
	case 4
		left = right - round(1.0/AspectRatio(listmode) * (bottom - top - framesy)) - framesx
		swbreak
	swend

	scrsize(0,listmode) = ginfo_winx ,round(AspectRatio(listmode)*ginfo_winx)
	cellsize(0,listmode) = round(1.0*scrsize(0,listmode) / (cellnum(0,listmode)+1)) ,round(1.0*scrsize(1,listmode) / (cellnum(1,listmode)+1))
	gosub *draw
	
	oncmd 1
return 1

	
//設定――――――――――――――――――――――――――――――――――
*listsavedialog
	
	BrowseFolder "一覧の保存先",(dir_exe)
	if stat = 1{
		listsavedir = refstr
		objprm listsdid,listsavedir
	}

return

*sssavedialog
	
	BrowseFolder "SSのキャプチャ保存先",(dir_exe+"\\ScreenShots")
	if stat = 1{
		sssavedir = refstr
		objprm sssdid,sssavedir
	}

return

*yabumiuploaderdialog
	
	dialog "exe",16,"YabumiUpLoader"
	if stat = 1{
		yabumidir = refstr
		objprm yabumiid,yabumidir
	}

return

*listsavefopen
	exec listsavedir,16
return

*sssavefopen
	exec sssavedir,16
return

*iniload
	
	SetIni (dir_exe+"\\makelist.ini")
	if stat = 1{
		sdim settingstr,1024
		settingstr = {"
			[Setting]
			yubiwa=1
			kantai=1
			autosearch=1
			hidename=0
			namecut=0
			frontrow=1
			hidelog=0
			yabumiautoupload=1
			othergame=0
			hotkeycap=1
			autoaddcaptcha=1
			jpgsave=0
			jpgquality=90
			timersec=5
			listsavedir=""
			sssavedir=""
			yabumidir=""
			hotkeystr=A___Z"}
		bsave (dir_exe+"\\makelist.ini"),settingstr,strlen(settingstr)+1024
	}

	
	GetIni "Setting", "yubiwa", yubiwa
	if (yubiwa != 0) & (yubiwa != 1):tempint = 1:WriteIni "Setting", "yubiwa",tempint

	GetIni "Setting", "kantai", kantai
	if (kantai != 0) & (kantai != 1):tempint = 1:WriteIni "Setting", "kantai",tempint
	
	GetIni "Setting", "autosearch", autosearch
	if (autosearch != 0) & (autosearch != 1):tempint = 1:WriteIni "Setting", "autosearch",tempint

	GetIni "Setting", "hidename", hidename
	if (hidename != 0) & (hidename != 1):tempint = 0:WriteIni "Setting", "hidename",tempint
	////
	GetIni "Setting", "namecut", namecut
	if (namecut != 0) & (namecut != 1):tempint = 0:WriteIni "Setting", "namecut",tempint
	
	GetIni "Setting", "frontrow", frontrow
	if (frontrow != 0) & (frontrow != 1):tempint = 1:WriteIni "Setting", "frontrow",tempint
	
	GetIni "Setting", "hidelog", hidelog
	if (hidelog != 0) & (hidelog != 1):tempint = 0:WriteIni "Setting", "hidelog",tempint

	GetIni "Setting", "yabumiautoupload", yabumiautoupload
	if (yabumiautoupload != 0) & (yabumiautoupload != 1):tempint = 1:WriteIni "Setting", "yabumiautoupload",tempint
	;yabumiautoupload
	GetIni "Setting", "othergame", othergame
	if (othergame != 0) & (othergame != 1):tempint = 0:WriteIni "Setting", "othergame",tempint
	
	GetIni "Setting", "hotkeycap", hotkeycap
	if (hotkeycap != 0) & (hotkeycap != 1):tempint = 1:WriteIni "Setting", "hotkeycap",hotkeycap

	GetIni "Setting", "autoaddcaptcha", autoaddcaptcha
	if (autoaddcaptcha != 0) & (autoaddcaptcha != 1):tempint = 0:WriteIni "Setting", "autoaddcaptcha",autoaddcaptcha

	GetIni "Setting", "jpgsave", jpgsave
	if (jpgsave != 0) & (jpgsave != 1):tempint = 0:WriteIni "Setting", "jpgsave",tempint
	
	GetIni "Setting", "jpgquality", jpgquality
	if (jpgquality < 1) | (jpgquality > 100) :jpgquality = 90
	tempint = jpgquality
	WriteIni "Setting", "jpgquality",tempint
	
	GetIni "Setting", "timersec", timersec
	if (timersec < 0) | (timersec > 999):timersec = 5
	tempint = timersec
	WriteIni "Setting", "timersec",tempint
	
	////
		
	GetIni "Setting", "listsavedir", listsavedir
	if PathIsDirectory(listsavedir){
	} else {
		listsavedir = dir_exe
		tempstr = ""
		WriteIni "Setting", "listsavedir", tempstr
	}
	
	GetIni "Setting", "sssavedir", sssavedir
	if PathIsDirectory(sssavedir){
	} else {
		sssavedir = dir_exe+"\\ScreenShots"
		if PathIsDirectory(sssavedir){
		} else {
			chdir dir_exe
			mkdir "ScreenShots"
		}
		tempstr = ""
		WriteIni "Setting", "sssavedir", tempstr
	}
	
	GetIni "Setting", "yabumidir", yabumidir
	if kmexist(yabumidir) = -1{
		yabumidir = dirinfo(0x10026) +"\\yabumi\\yabumiuploader.exe"
		if kmexist(yabumidir) = -1{
			availableyabumi = 0
			yabumidir = ""
			tempstr = ""
			WriteIni "Setting", "yabumidir", tempstr
		} else {
			availableyabumi = 1
			tempstr = yabumidir
			WriteIni "Setting", "yabumidir", tempstr
		}
	} else {
		availableyabumi = 1
		tempstr = yabumidir
		WriteIni "Setting", "yabumidir", tempstr
	}
			
	GetIni "Setting", "hotkeystr", hotkeystr
	if peek(hotkeystr,0) = 'A':hotkeycapalt=1
	if peek(hotkeystr,1) = 'C':hotkeycapctrl=1
	if peek(hotkeystr,2) = 'S':hotkeycapshift=1
	if peek(hotkeystr,3) = 'W':hotkeycapwin=1
	hotkeycapchar = strmid(hotkeystr,4,1);strf("%c",peek(hotkeystr,4))
	
return
		
*inisave

	savelogtext = ""

	if (yubiwa = 0) | (yubiwa = 1){
		tempint = yubiwa
		WriteIni "Setting", "yubiwa",tempint
	} else {
		tempint = 1
		WriteIni "Setting", "yubiwa",tempint
	}

	if (kantai = 0) | (kantai = 1){
		tempint = kantai
		WriteIni "Setting", "kantai",tempint
	} else {
		tempint = 1
		WriteIni "Setting", "kantai",tempint
	}

	if (autosearch = 0) | (autosearch = 1){
		tempint = autosearch
		WriteIni "Setting", "autosearch",tempint
	} else {
		tempint = 1
		WriteIni "Setting", "autosearch",tempint
	}

	if (hidename = 0) | (hidename = 1){
		tempint = hidename
		WriteIni "Setting", "hidename",tempint
	} else {
		tempint = 0
		WriteIni "Setting", "hidename",tempint
	}

	if (namecut = 0) | (namecut = 1){
		tempint = namecut
		WriteIni "Setting", "namecut",tempint
	} else {
		tempint = 0
		WriteIni "Setting", "namecut",tempint
	}

	if (frontrow = 0) | (frontrow = 1){
		tempint = frontrow
		WriteIni "Setting", "frontrow",tempint
	} else {
		tempint = 1
		WriteIni "Setting", "frontrow",tempint
	}
	
	if (hidelog = 0) | (hidelog = 1){
		tempint = hidelog
		WriteIni "Setting", "hidelog",tempint
	} else {
		tempint = 0
		WriteIni "Setting", "hidelog",tempint
	}
	;yabumiautoupload
	if (yabumiautoupload = 0) | (yabumiautoupload = 1){
		tempint = yabumiautoupload
		WriteIni "Setting", "yabumiautoupload",tempint
	} else {
		tempint = 1
		WriteIni "Setting", "yabumiautoupload",tempint
	}
	if (othergame = 0) | (othergame = 1){
		tempint = hidelog
		WriteIni "Setting", "othergame",othergame
	} else {
		tempint = 0
		WriteIni "Setting", "othergame",othergame
	}
		
	if (hotkeycap = 0) | (hotkeycap = 1){
		tempint = hotkeycap
		WriteIni "Setting", "hotkeycap",tempint
	} else {
		tempint = 1
		WriteIni "Setting", "hotkeycap",tempint
	}
	
	if (autoaddcaptcha = 0) | (autoaddcaptcha = 1){
		tempint = autoaddcaptcha
		WriteIni "Setting", "autoaddcaptcha",tempint
	} else {
		tempint = 0
		WriteIni "Setting", "autoaddcaptcha",tempint
	}

	if (jpgsave = 0) | (jpgsave = 1){
		tempint = jpgsave
		WriteIni "Setting", "jpgsave",tempint
	} else {
		tempint = 0
		WriteIni "Setting", "jpgsave",tempint
	}
	
	if (jpgquality < 1) | (jpgquality > 100):jpgquality = 90
	tempint = jpgquality
	WriteIni "Setting", "jpgquality",tempint
	
	if (timersec < 0) | (timersec > 999):timersec = 5
	tempint = timersec
	WriteIni "Setting", "timersec",tempint

	if PathIsDirectory(listsavedir) {
		tempstr = listsavedir
		WriteIni "Setting", "listsavedir", tempstr
	} else {
		savelogtext += "Info - 一覧保存先が存在しないためデフォルトに変更しました\n"
		listsavedir = dir_exe
		tempstr = listsavedir
		WriteIni "Setting", "listsavedir", tempstr
		objprm listsdid,listsavedir
	}
	
	if PathIsDirectory(sssavedir) {
		tempstr = sssavedir
		WriteIni "Setting", "sssavedir", tempstr
	} else {
		savelogtext += "Info - SS保存先が存在しないためデフォルトに変更しました\n"
		chdir dir_exe
		if PathIsDirectory(dir_exe+"\\ScreenShots") = 0 {
			mkdir "ScreenShots"
		}
		sssavedir = dir_exe+"\\ScreenShots"
		tempstr = sssavedir
		WriteIni "Setting", "sssavedir", tempstr
		objprm sssdid,sssavedir
	}

	if kmexist(yabumidir) = -1{
		yabumidir = dirinfo(0x10026) +"\\yabumi\\yabumiuploader.exe"
		if kmexist(yabumidir) = -1{
			availableyabumi = 0
			yabumidir = ""
			tempstr = ""
			WriteIni "Setting", "yabumidir", tempstr
		} else {
			availableyabumi = 1
			tempstr = yabumidir
			WriteIni "Setting", "yabumidir", tempstr
		}
	} else {
		availableyabumi = 1
		tempstr = yabumidir
		WriteIni "Setting", "yabumidir", tempstr
	}
		
	;*/

	hotkeystr = ""
		
	if (hotkeycapalt + hotkeycapctrl + hotkeycapshift + hotkeycapwin) = 0{
		savelogtext += "Info - ホットキーの設定が不適切です(1) 設定をデフォルトに変更しました\n"
		hotkeycap = 0
		hotkeycapalt = 1
		hotkeycapctrl = 0
		hotkeycapshift= 0
		hotkeycapwin = 0
	}
	
	if hotkeycapalt	= 1	:poke hotkeystr,0,'A':else:poke hotkeystr,0,95
	if hotkeycapctrl = 1:poke hotkeystr,1,'C':else:poke hotkeystr,1,95
	if hotkeycapshift= 1:poke hotkeystr,2,'S':else:poke hotkeystr,2,95
	if hotkeycapwin	= 1	:poke hotkeystr,3,'W':else:poke hotkeystr,3,95
	
	if (65 <= peek(hotkeycapchar)) & (peek(hotkeycapchar) <= 90){
		hotkeycode = peek(hotkeycapchar)
		poke hotkeystr,4,hotkeycode
	} else {
		if (97 <= peek(hotkeycapchar)) & (peek(hotkeycapchar) <= 122){
			hotkeycode = peek(hotkeycapchar)-32
			hotkeycapchar = strf("%c",hotkeycode)
			poke hotkeystr,4,hotkeycode
		} else {
			savelogtext += "Info - ホットキーの設定が不適切です(2) 設定をデフォルトに変更しました\n"
			hotkeycode = 90
			hotkeycapchar = strf("%c",hotkeycode)
			poke hotkeystr,4,hotkeycode
			hotkeycap = 0
		}
	}
	WriteIni "Setting", "hotkeystr", hotkeystr

	if savelogtext != ""{
		savelogtext += "makelist.iniを保存しました"
		dialog savelogtext
	}
	
	;dialog hotkeystr
	
return

*hotkeycapsetting
;/*
	if (hotkeycap = 1) & (hotkeystr != hotkeystr_temp) {
		UnregisterHotKey hwnd0,HOTKEYID
		RegisterHotKey hwnd0,HOTKEYID,(MOD_ALT*hotkeycapalt)|(MOD_CONTROL*hotkeycapctrl)|(MOD_SHIFT*hotkeycapshift)|(MOD_WIN*hotkeycapwin),hotkeycode
		hotkeystr_temp = hotkeystr
		if stat = 0{
			;/*
			logmessage = "ホットキーの設定に失敗しました"
			objprm logid,logmessage
			hotkeyset = 0
			gsel 0,0
			title "メイン"
			;*/
		} else {
			logmessage = "ホットキーを設定しました"
			objprm logid,logmessage
			hotkeyset = 1
			gsel 0,0
			title "*メイン"
		}
	}
	
	if (hotkeycap = 0) & (hotkeyset = 1){
		UnregisterHotKey hwnd0,HOTKEYID
		logmessage = "ホットキーを解除しました"
		objprm logid,logmessage
		hotkeyset = 0
		gsel 0,0
		title "メイン"
	}
;*/
return


*disinfoinit
	
	disinfo(0) = GetSystemMetrics(76)
	disinfo(1) = GetSystemMetrics(77)
	disinfo(2) = GetSystemMetrics(78)
	disinfo(3) = GetSystemMetrics(79)

	init_makelist disinfo
	
return


*en_d
	
	if wparam = 1{
		gosub *ssmode
		objprm comboxid,0
		return
	}
	if wparam = 12{
		gsel 12,-1
		gosub *ssmode
		objprm comboxid,0
		return
	}
	
	DragAcceptFiles hwnd1, 0
	DragAcceptFiles hwnd0, 0
	
	if seqcapf = 1{
		KillTimer hwnd0,timerID
	}
	gosub *inisave
	if (hotkeycap = 0) & (hotkeyset = 1){
		UnregisterHotKey hwnd0,HOTKEYID
	}
	end