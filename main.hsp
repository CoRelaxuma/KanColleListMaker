/*
* SoftWareName(Jp)	: 艦これ一覧めいかー
* SoftWareName(En)	: KanColleListMaker
* Version			: 1.2.2
* Author			: kanahiron
* License			: MIT
* Copyright(C) 2014 kanahiron
*/

#uselib "shlwapi.dll"
#cfunc PathIsDirectory "PathIsDirectoryA" sptr

#uselib "shell32.dll"
#func global DragAcceptFiles "DragAcceptFiles" int,int
#func global DragQueryFile   "DragQueryFileA"  int,int,int,int
#func global DragQueryPoint  "DragQueryPoint"  int,int
#func global DragFinish      "DragFinish"      int

#uselib "gdi32.dll"
#cfunc global CreateDC "CreateDCA" sptr,sptr,sptr,int
#func global DeleteDC "DeleteDC"  int
#func global GetDC "GetDC" int
#func global ReleaseDC "ReleaseDC" int,int
#func global BitBlt "BitBlt" int,int,int,int,int,int,int,int,int
#func global CreateDIBSection "CreateDIBSection" int,int,int,int,int,int
#func global CreateCompatibleBitmap "CreateCompatibleBitmap" int,int,int
#func global SelectObject "SelectObject" int,int
#func global DeleteObject "DeleteObject" int 

#uselib "user32.dll"
#func global MoveWindow "MoveWindow" int , int , int , int , int , int
#func global GetWindowLong "GetWindowLongA" int,int
#func global SetWindowLong "SetWindowLongA" int,int,int
#func global SetLayeredWindowAttributes "SetLayeredWindowAttributes" int,sptr,int,int
#func global GetCursorPos "GetCursorPos" int
#func global SetCursorPos "SetCursorPos" int,int
#func global ScreenToClient "ScreenToClient" int ,int
#func global ChildWindowFromPoint "ChildWindowFromPoint" int ,int
#func global SetFocus "SetFocus" int
#func global SetParent "SetParent" int ,int
#func global RegisterHotKey "RegisterHotKey" int,int,int,int
#func global UnregisterHotKey "UnregisterHotKey" int,int
#cfunc global IsWindowVisible "IsWindowVisible" int
#cfunc global GetSystemMetrics "GetSystemMetrics" int
#func global SetClassLong "SetClassLongA" int,int,int
#func global LoadCursor "LoadCursorA" int,int
#func global SetTimer "SetTimer" int,int,int,sptr
#func global KillTimer "KillTimer" int,int

#uselib "winmm.dll"
#cfunc global timeGetTime "timeGetTime"

#include "kmodule.as"
#include "ImageFileModule2.as"
#include "inimodule.as"
#include "exdialog.as"

;/*
#include "makelistmodule_Ver6.as"

#pack "data.png"
#packopt name "艦これ一覧めいかー Ver1_2_2"
#packopt hide 1

#define HOTKEYID	7789
#define timerID		100
#define ctype round(%1) int(strf("%%0.0f", %1))

	onexit gosub *en_d

	mode = 0
	listmode = 0
	yubiwa = 1
	kantai = 1
	autosearch = 1
	hidename = 0
	sti = 0
	cliflag = 0
	cellmode = 0
	mesy = 30
	hotkeycap = 0
	ret = 0

	jpgquality = 90
	namecut = 0
	hidelog = 0
	jpgsave = 0
	rapidshooting = 0
	frontrow = 0
	othergame = 0
	
	availablecap = 0
	availableyabumi = 0
	hotkeyset = 0
	
	tempint = 0
	tempstr = ""
	
	dim memberlistxy,2
	dim data,8,5,2
	dim bc,50
	dim dd,4,2
	dim mesxy,2
	dim maxscrwh,2
	dim mindexxy,2
	dim mxy,2
	dim mxy_,2
	dim dmxy,2
	dim sscap,4
	dim sscapwh,2
	dim cellnum,2,2
	dim erasecell,2,2
	dim scrsize,2,2
	dim cellsize,2,2
	dim tempmouse,2
	dim disinfo,4
	dim ecdd,4,2
	
	dim scrsizetemp,2,2
	
	ddim Aspectratio,2
	sdim sscapmes,256
	sdim logmessage,256
	sdim modemes,64,4
	sdim stemp
	sdim sssavedir ,260
	sdim listsavedir ,260
	sdim yabumidir,260
	sdim savekind,64,2
	sdim hotkeycapchar
	sdim hotkeystr
	sdim hotkeystr_temp
	sdim savelogtext,1024
	cellnum(0,0) = 6,4
	cellnum(0,1) = 5,2
	ecdd(0,0) = 0,38,194,138
	ecdd(0,1) = 0,176,228,182
	erasecell(0,0) = 6,4
	erasecell(0,1) = 5,2
	bc(0) = 20,200
	maxscrwh(0) = ginfo(20),ginfo(21)
	mindexxy(0) = 0,0
	modemes(0) = "SSキャプチャ","艦娘一覧","攻略編成","オプション"
	mxy(0) = 0,0
	mxy_(0) = 0,0
	sscapwh = 0,0
	seccapf = 0
	sscapmes = "取得する"
	savekind = ".png",".jpg"
	seqcapmes = "開始"
	luposx = 0
	luposy = 0
	rdposx = 0
	rdposy = 0
	
	
	scrsize(0,0) = limit(1358,210,round(0.66*maxscrwh(0))),scrsize(0,0)*aspectratio(0)
	scrsize(0,1) = limit(1120,250,round(0.66*maxscrwh(0))),scrsize(0,1)*aspectratio(1)
	scrsizetemp(0,0)  = scrsize(0,0),scrsize(1,0)
	scrsizetemp(0,1)  = scrsize(0,1),scrsize(1,1)
	
	gosub *iniload
	gosub *inisave
	gosub *disinfoinit
	
	
	buffer 12,510,270
	font "メイリオ",30,1
	string = "第一艦隊　 　1\n　　　　 　　2\n　　　　　 　3\n　　　　　　 4\n　　　　　 　5\n　　　　 　　6"
	mref BMSCR, 67
	x = ginfo_cx+10
	y = ginfo_cy
	y2 = y
	r = 0.1 * abs(BMSCR.49)
	n = int(2.0 * M_PI * r)
	color 
	boxf
	color 255,255,255
	repeat n
		rad = 2.0 * M_PI * cnt / n
		pos x + int(r * cos(rad)), y + int(r * sin(rad))
		mes string
	loop
	color 255,0,0
	pos x, y
	mes string
	x = ginfo_cx+260
	y = y2
	string = "第二艦隊　 　1\n　　　　　 　2\n　　　 　　　3\n　　　 　　　4\n　　　　 　　5\n　　 　　　　6"
	color 255,255,255
	repeat n
		rad = 2.0 * M_PI * cnt / n
		pos x + int(r * cos(rad)), y + int(r * sin(rad))
		mes string
	loop
	color 0,0,255
	pos x, y
	mes string 
	
	
	buffer 10,disinfo(2),disinfo(3)
	chgbm 32
	color 255,255,255
	boxf
	redraw
	//手動選択重ね用
	bgscr 11,disinfo(2),disinfo(3),2
	hwnd11 = hwnd
	font "メイリオ",30

	//範囲選択用レイヤードウィンドウ
	bgscr 9,ginfo(20),ginfo(21),2
	hwnd9 = hwnd
	color 0,255,0
	boxf
	GetWindowLong hwnd9,-20
	SetWindowLong hwnd9,-20,stat|$80000
	SetLayeredWindowAttributes hwnd9,0x00000000,128,LWA_ALPHA

	screen 1,maxscrwh(0),maxscrwh(1),2:title "表示させたいセルにD&D"
	hwnd1 = hwnd
	DragAcceptFiles hwnd1, 0
	oncmd gosub *OnDropFiles, WM_DROPFILES
	oncmd gosub *windowsize, WM_SIZING
	onclick gosub *listdrag
	framesx = ginfo_sizex - ginfo_winx
	framesy = ginfo_sizey - ginfo_winy
	
	//一覧モード出力用
	buffer 2,disinfo(2),disinfo(3)

	//画像読み込み用
	buffer 7
	picload "data.png"
	
	;//キャプチャ作成用
	buffer 8
	//キャプ用
	buffer 5,disinfo(2),disinfo(3)

	//背景コピー用
	buffer 4,maxscrwh(0),maxscrwh(1)
	//縮小処理用
	buffer 3,maxscrwh(0)/5,maxscrwh(1)/3
	
	screen 0,350,600:title "メイン"
	hwnd0 = hwnd
	DragAcceptFiles hwnd0, 0
	oncmd gosub *OnDropFiles ,WM_DROPFILES
	oncmd gosub *setmode ,WM_COMMAND
	oncmd gosub *hotkey ,WM_HOTKEY
	oncmd gosub *sscaptcha,0x0113
	
	color 240,240,240:boxf:color
	font "メイリオ",13
	objmode 2
	
	objsize 175,30
	pos 0,0
	combox cellmode,30,"  SSキャプチャモード\n  艦娘一覧モード\n  攻略編成モード\n  オプション"
	comboxid = stat
	hcombox = objinfo_hwnd(comboxid)
	objsize 0,0
	button "dum_my",*adummy
	
	gosub *ssmode
	gosub *hotkeycapsetting

	if autosearch {
		gsel 0,-1
		wait 50
		gosub *sssetting
		gsel 0,1+frontrow
	}

	stop

*adummy
	
*setmode
	oncmd 0

	if (lparam = hcombox) & (((wparam>>16) & 0x0000FFFF) = 1){
	
		if mode = 1 | mode = 2{
			DragAcceptFiles hwnd1, 0
			DragAcceptFiles hwnd0, 0
			gsel 1,0
			onclick 0
		}
	
		if mode = 3{
			gosub *inisave				
			gosub *hotkeycapsetting
			gosub *disinfoinit
		}
	
		sendmsg hCombox, $147
		mode = stat
	
		if mode = 1 | mode = 2{
			DragAcceptFiles hwnd1, 1
			DragAcceptFiles hwnd0, 1
		}
	
		if mode = 0{
			gosub *ssmode
		}
		if mode = 1{
			listmode = 0
			gsel 1,0
			onclick 1
			gosub *list
		}
		if mode = 2{
			listmode = 1
			gsel 1,0
			onclick 1
			gosub *list
		}
		if mode = 3{
			gosub *option
		}
	} else {
		
		if mode = 1{	
			if (lparam = yubiwach) | (lparam = kantaich) | (lparam = pagech) | (lparam = lvnamech) {
				gosub *listchange
			}
		}
	
		if mode = 2{	
			if (lparam = sosukach) | (lparam = sosuch) | (lparam = sokach) | (lparam = tuika1ch) | (lparam = tuika2ch) | (lparam = rengoumodech) {
				gosub *listchange
			}
		}
		
	}
	
	
	oncmd 1
return

*listchange
	
	if mode = 1{
	
		lvnamef = lvname
		
		w = 0
		h = 0
		capx = 0
		capy = 0
		cellratio = 0,0
		sendmsg yubiwach, $F2
		yubiwa = stat & 0x01
		sendmsg kantaich, $F2
		kantai = stat & 0x01
		sendmsg pagech, $F2
		page = stat & 0x01
		sendmsg lvnamech, $F2
		lvname = stat & 0x01
		
		if yubiwa = 0 & kantai = 0 : w = 382 : capx = 392
		if yubiwa = 1 & kantai = 1 : w = 440 : capx = 360
		if yubiwa = 1 & kantai = 0 : w = 408 : capx = 392
		if yubiwa = 0 & kantai = 1 : w = 415 : capx = 360
		if page = 0:h = 279
		if page = 1:h = 315
		if lvname = 1:w = 190: capx = 392:h = 279
		capy = 154
	
		dd(0,0) = capx,capy,w,h
		cellratio = 1.0*h/w

		if lvname = 0{
			if lvnamef = 1{
				scrsize(0,0) = scrsizetemp(0)
			}
			cellsize(0,0) = scrsize(0,0)/(cellnum(0,0)+1),round(cellratio*cellsize(0,0))
			scrsize(1,0) = round(cellsize(1,0)* (cellnum(1,0)+1) )
			
		} else {
			if lvnamef = 0{
				scrsizetemp(0,0)  = scrsize(0,0)
				cellsize(1,0) = scrsize(1,0)/(cellnum(1,0)+1)
				cellsize(0,0) = round(1.0*cellsize(0,0)/cellratio)
				scrsize(0,0) = round(cellsize(0,0)* (cellnum(0,0)+1) )
			}
		}
		AspectRatio(0,0) = 1.0*scrsize(1,0)/scrsize(0,0)
		gsel 1
		width scrsize(0,0),scrsize(1,0)
	}
	
	
	if mode = 2{
	
		sosuf = sosu
	
		w = 0
		h = 0
		capx = 0
		capy = 0
		cellratio = 0,0
		sendmsg sosukach, $F2
		sosuka = stat & 0x01
		sendmsg sosuch, $F2
		sosu = stat & 0x01
		sendmsg sokach, $F2
		soka = stat & 0x01
	
		sendmsg tuika1ch, $F2
		tuika1 = stat & 0x01
		sendmsg tuika2ch, $F2
		tuika2 = stat & 0x01
	
		sendmsg rengoumodech, $F2
		rengoumode = stat & 0x01
	
		if sosuka {
			w = 460
			h = 365
		}
		if sosu {
			w = 230
			h = 365	
		}
		if soka {
			w = 460
			h = 195
		}
		capx = 327
		capy = 103
	
		dd(0,1) = capx,capy,w,h
		cellratio = 1.0*h/w
	

		if sosuka | soka{
			if sosuf = 1{
				scrsize(0,1) = scrsizetemp(0,1)
			}
			cellsize(0,1) = scrsize(0,1)/(cellnum(0,1)+1),round(cellratio*cellsize(0,1))
			scrsize(1,1) = round(cellsize(1,1)* (cellnum(1,1)+1) )
			
		} else {
			if sosuf = 0{
				scrsizetemp(0,1)  = scrsize(0,1)
				cellsize(1,1) = scrsize(1,1)/(cellnum(1,1)+1)
				cellsize(0,1) = round(1.0*cellsize(1,1)/cellratio)
				scrsize(0,1) = round(cellsize(0,1) * (cellnum(0,1)+1) )
			}
		}

		AspectRatio(1) = 1.0*scrsize(1,1)/scrsize(0,1)
		gsel 1
		width scrsize(0,1),scrsize(1,1)
	}

	
	gosub *draw
		
return


//SSモード――――――――――――――――――――――――――――――――――
*ssmode
	
	gsel 1,-1
	gsel 0,1+frontrow
	clrobj 1
	color 240,240,240:boxf:color

	mesy = 31
	
	pos 2,mesy
	mes "キャプチャ座標"
		
	objsize 80,25
	pos 95,mesy-3
	button gosub sscapmes,*sssetting
	sscapiid = stat
	mesy+=22
	
	
	
	if manualpos {
		if manualpos {
			manualposf = 0
		} else {
			manualposf = 1
		}
		pos 84+(manualposf*200),mesy+5
		mes "x"
		pos 0+(manualposf*200),mesy
		input luposx,40,25,5
		luposxiid = stat
		pos 40+(manualposf*200),mesy
		input luposy,40,25,5
		luposyiid = stat
		pos 95+(manualposf*200),mesy
		input rdposx,40,25,5
		rdposxiid = stat
		pos 135+(manualposf*200),mesy
		input rdposy,40,25,5
		rdposyiid = stat
		mesy+=25
	}
		

		
	objsize 150-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),30
	pos 0,mesy
	button gosub "SSをキャプチャする",*sscaptcha
	sscapbid = stat

	objsize 25,30
	pos 150-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),mesy
	button gosub "開",*sssavefopen
	fopenbid = stat
	
	if ((yabumiautoupload = 1) & (availableyabumi = 1)) {
		pos 150,mesy
	} else {
		pos 200,mesy
	}	
	button gosub "UP",*yabumiupload
	yabumibid = stat
	
	mesy+=29
	
	pos 2+(disableseqcap*-175),mesy+2
	mes "連続キャプチャ"
	objsize 80,25
	pos 95+(disableseqcap*-175),mesy
	if disableseqcap = 0:mesy+=24
	button gosub seqcapmes,*seqcap
	seqcapbid = stat
	
	objsize 175,20
	pos 5,mesy
	chkbox "座標自動検索",autosearch:mesy+=20
	pos 5,mesy
	chkbox "提督名と司令部Lvを隠す",hidename:mesy+=20
	
	dispcapy = mesy
	if dispcap {
		color 200,200,200
		boxf 0,mesy,175,mesy+105
		color 240,240,240
		boxf 3,mesy+3,171,mesy+101
		color
		mesy += 105
	}

	if hidelog = 0{
		pos 0,mesy
		mesy += 70
	} else {
		pos -175,mesy
	}
	mesbox logmessage,175,70,0
	logid = stat

	if availablecap = 0{
		objenable sscapbid,0
		objenable seqcapbid,0
		objenable yabumibid,0
	} else {
		objenable sscapbid,1
		objenable seqcapbid,1
		objenable yabumibid,1
	}
	
	if manualpos {
		objenable sscapbid,1
	}
	
	width 175,mesy
	
return

//リストモード――――――――――――――――――――――――――――――――――
*list
	
	mxy(0) = 0,0
	mxy_(0) = 0,0
	cliflag = 0
	mesy = 28

	gsel 0,2
	clrobj 1
	color 240,240,240:boxf:color
	
	objsize 175,20
	

	if listmode = 0{	
		
		pos 5,mesy
		chkbox "指輪も範囲に入れる",yubiwa:mesy+=20
		yubiwacid = stat
		yubiwach = objinfo_hwnd(yubiwacid)
		
		pos 5,mesy
		chkbox "所属艦隊も範囲に入れる",kantai:mesy+=20
		kantaicid = stat
		kantaich = objinfo_hwnd(kantaicid)
		
		pos 5,mesy
		chkbox "ページ数も範囲に入れる",page:mesy+=20
		pagecid = stat
		pagech = objinfo_hwnd(pagecid)
		pos 5,mesy
		
		chkbox "レベルと艦名のみにする",lvname:mesy+=20
		lvnamecid = stat
		lvnamech = objinfo_hwnd(lvnamecid)
	}
	if listmode = 1{
	
		mesy = mesy+2
		pos 5,mesy
		chkbox "連合艦隊の区切線を追加",rengoumode:mesy+=20
		rengoumodecid = stat
		rengoumodech = objinfo_hwnd(rengoumodecid)
	
		pos 5,mesy+1
		mes "自動追加の方法":mesy+=18
	
		pos 10,mesy
		chkbox "横2縦3",tuika1:mesy+=20
		tuika1cid = stat
		tuika1ch = objinfo_hwnd(tuika1cid)
		SetWindowLong tuika1ch, -16, $50000009 | $20000
	
		pos 10,mesy
		chkbox "横6縦1",tuika2:mesy+=20
		tuika2cid = stat
		tuika2ch = objinfo_hwnd(tuika2cid)
		sendmsg tuika2ch, $F4, $9
	
		pos 5,mesy+1
		mes "キャプチャ範囲":mesy+=18
	
		pos 10,mesy
		chkbox "装備とステータスと艦娘",sosuka:mesy+=20
		sosukacid = stat
		sosukach = objinfo_hwnd(sosukacid)
		SetWindowLong sosukach, -16, $50000009 | $20000
	
		pos 10,mesy
		chkbox "装備とステータス",sosu:mesy+=20
		sosucid = stat
		sosuch = objinfo_hwnd(sosucid)
		sendmsg sosuch, $F4, $9
	
		pos 10,mesy
		chkbox "装備と艦娘の画像",soka:mesy+=21
		sokacid = stat
		sokach = objinfo_hwnd(sokacid)
		sendmsg sokach, $F4, $9
		
	
	}
			
	objsize 150-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),30
	pos 0,mesy
	button gosub "作成",*make
	
	objsize 25,30
	pos 150-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),mesy
	button gosub "開",*listsavefopen
	fopenbid = stat
	
	if ((yabumiautoupload = 1) & (availableyabumi = 1)) {
		pos 150,mesy
	} else {
		pos 200,mesy
	}	
	button gosub "UP",*yabumiupload
	yabumibid = stat
	
	mesy+=30

	pos 0,mesy
	mesbox logmessage,175,70,0 :mesy+=70
	logid = stat
	
	width 175,mesy-(70*hidelog)
	
	if IsWindowVisible(hwnd1) = 0{
		gsel 1,1
	} else {
		gsel 1,0
	}
	title modemes(listmode+1)+"モード - 表示させたいセルにD&D"
	width scrsize(0,listmode),scrsize(1,listmode)
	
	gosub *draw
	
return
	
//オプション――――――――――――――――――――――――――――――――――
*option

	gsel 1,-1
	gsel 0,1+frontrow
	clrobj 1
	color 240,240,240:boxf:color
	
	mesy = 30
	pos 4,mesy
	mes "readmeをよくお読み下さい":mesy+=20
	
	objsize 350,20
	pos 5,mesy
	chkbox "提督名を隠すのではなくカットする",namecut:mesy+=20
	pos 5,mesy
	chkbox "常にメインウィンドウを最前列表示",frontrow:mesy+=20
	pos 5,mesy
	chkbox "ログを非表示",hidelog:mesy+=20
	pos 5,mesy
	chkbox "yabumiに自動でアップロードする",yabumiautoupload:mesy+=20
	pos 5,mesy
	chkbox "艦これ以外も手動で取得可能にする",othergame:mesy+=20
	pos 5,mesy
	chkbox "キャプチャのプレビューを有効にする",dispcap:mesy+=20
	pos 5,mesy
	chkbox "取得座標を手動で入力できるようにする(暫定)",manualpos:mesy+=20
	pos 5,mesy
	chkbox "ホットキーでSSをキャプチャする",hotkeycap:mesy+=20

	objsize 50,20
	pos 30,mesy
	chkbox "Alt",hotkeycapalt
	pos 80,mesy
	chkbox "Ctrl",hotkeycapctrl
	pos 130,mesy
	chkbox "Shift",hotkeycapshift
	pos 180,mesy
	chkbox "Win",hotkeycapwin
	pos 240,mesy
	input hotkeycapchar,20,20,1
	pos 265,mesy
	mes "キー":mesy+=20
	
	objsize 350,20
	pos 5,mesy
	chkbox "＊一覧モードでSSを自動追加する",autoaddcaptcha:mesy+=20
	pos 5,mesy
	chkbox "jpgで保存する",jpgsave:mesy+=20
	
	pos 5,mesy
	mes "jpg保存品質"
	pos 155,mesy
	mes "1〜100(大きいほど高画質)"
	pos 100,mesy
	input jpgquality,50,20,3:mesy+=20
	
	pos 5,mesy
	chkbox "連続キャプチャを無効にする",disableseqcap:mesy+=20
	
	pos 5,mesy
	mes "連続キャプチャの間隔"
	pos 205,mesy
	mes "x0.1秒 (0.1〜99.9秒)"
	pos 150,mesy
	input timersec,50,20,3:mesy+=20

	objsize 150,20
	pos 14,mesy
	mes "一覧保存先"
	pos 200,mesy
	button gosub "一覧保存先変更",*listsavedialog:mesy+=20
	pos 0,mesy
	mesbox listsavedir,350,45,5,259:mesy+=45
	listsdid = stat
	
	pos 14,mesy
	mes "SS保存先"
	pos 200,mesy
	button gosub "SS保存先変更",*sssavedialog:mesy+=20
	pos 0,mesy
	mesbox sssavedir,350,45,5,259:mesy+=45
	sssdid = stat
	
	pos 14,mesy
	mes "YabumiUploader.exeの場所"
	pos 200,mesy
	button gosub "変更",*yabumiuploaderdialog:mesy+=20
	pos 0,mesy
	mesbox yabumidir,350,45,5,259:mesy+=45
	yabumiid = stat
	
	width 350,mesy

return


//SSキャプチャセッティング――――――――――――――――――――――――――――――――――
*sssetting
	
	objenable comboxid,0
	objenable sscapbid,0
	objenable sscapiid,0
	objenable seqcapbid,0
	if (yabumiautoupload = 1) & (availableyabumi = 1){
		objenable yabumibid,0
	}
	wait 20
	
	gsel 10
	redraw 0
	BitBlt hdc,0,0, disinfo(2), disinfo(3), hdcScreen,  disinfo(0), disinfo(1),SRCCOPY
	redraw 1

	if autosearch = 0 {
		gsel 11,-1
		pos 0,0
		gcopy 10,0,0,disinfo(2),disinfo(3)
		color 255,255,255
		boxf abs(disinfo(0)),abs(disinfo(1)),abs(disinfo(0))+970,abs(disinfo(1))+45
		color
		pos abs(disinfo(0))+5,abs(disinfo(1))+2
		mes "艦これの画面を覆うようにドラッグしてください Escキーでキャンセル"
		gsel 11,2
		MoveWindow hwnd11,disinfo(0),disinfo(1),disinfo(2),disinfo(3),1
		LoadCursor 0, 32515
		SetClassLong hwnd, -12, stat
		getkancollewindowposmanual 10,sscap,9
		gsel 11,-1
		LoadCursor 0, 32512
		SetClassLong hwnd, -12, stat
	} else {
		gsel 0
		logmessage = "座標自動取得中です"
		objprm logid,logmessage:await 

		getkancollewindowposauto2 10,sscap
		if stat = -1{
			if availablecap {
				dialog "自動座標取得に失敗しました\n前回の座標を残しますか？",2,"確認"
				if stat = 7{
					sscap(0) = 0,0,0,0
					availablecap = 0
				}
			}
				
				
		}
	}
	
	sscapwh(0) = sscap(2)-sscap(0),sscap(3)-sscap(1)
	luposx = sscap(0)
	luposy = sscap(1)
	rdposx = sscap(2)
	rdposy = sscap(3)
	
	gsel 0
	if manualpos{
		objprm luposxiid,luposx
		objprm luposyiid,luposy
		objprm rdposxiid,rdposx
		objprm rdposyiid,rdposy
	}
	
	gsel 0,1+frontrow
	objenable comboxid,1
	objenable sscapiid,1
	
	if ( (sscapwh(0) = 800) & (sscapwh(1) = 480) ) | ( (othergame=1) & (autosearch=0) ){
		sscapmes = strf("%d,%d",sscap(0),sscap(1))
		objprm sscapiid,sscapmes
		objenable sscapbid,1
		objenable seqcapbid,1
		logmessage = "座標の取得に成功しました"
		objprm logid,logmessage
		availablecap = 1
		objenable yabumibid,1

		
	} else {
		sscapmes = "取得する"
		objprm sscapiid,sscapmes
		objenable sscapbid,0
		objenable seqcapbid,0
		logmessage = "座標を取得できません\n詳細はreadmeをお読みください"
		objprm logid,logmessage
		availablecap = 0
		objenable yabumibid,0

	}
	
	
	if manualpos {
		objenable sscapbid,1
	}
	
	if hotkeyset = 1 & availablecap = 1 {
		gsel 0,0
		title "*メイン"
	} else {
		gsel 0,0
		title "メイン"
	}


return

//SSキャプチャ――――――――――――――――――――――――――――――――――
*sscaptcha
	oncmd 0
	
	gsel 0
	sscap(0) = luposx
	sscap(1) = luposy
	sscap(2) = rdposx
	sscap(3) = rdposy
	sscapwh(0) = sscap(2)-sscap(0),sscap(3)-sscap(1)
	sscapmes = strf("%d,%d",sscap(0),sscap(1))
	if mode = 0{
		objprm sscapiid,sscapmes
	}
	
	buffer 5,sscapwh(0),sscapwh(1)
	redraw 0
	BitBlt hdc, 0, 0, sscapwh(0), sscapwh(1), hdcScreen, sscap(0), sscap(1),SRCCOPY
	redraw 1

	if (homeport(5) = 1) & (hidename = 1) & (sscapwh(0) = 800) & (sscapwh(1) = 480) {

		pos 113,0
		gcopy 7,0,0,163,22
		pos 395,10
		gcopy 7,0,22,100,16

		if (namecut = 1) {
			buffer 5,800,450
			redraw 0
			BitBlt hdc, 0, 0, 800, 480,  hdcScreen, sscap(0), sscap(1)+30,SRCCOPY
			redraw 1
		}
	
	}
	
	savename = strf("kancolle_%04d%02d%02d-%02d%02d%02d%03d%s",gettime(0),gettime(1),gettime(3),gettime(4),gettime(5),gettime(6),gettime(7),savekind(jpgsave))
	gdiimagesave sssavedir +"\\"+ savename,jpgquality
	
	gsel 0
	
	if kmexist(sssavedir +"\\"+ savename) <= 0{
		logmessage = savename+"の保存に失敗しました"
		objprm logid,logmessage
	} else {
		logmessage = savename+"を保存しました"
		objprm logid,logmessage
		if dispcap & (mode=0){
			pos 0,dispcapy
			gzoom 175,105,5,0,0,sscapwh(0),sscapwh(1),1
		}
	}

	oncmd 1
return


*listdrag
	
	if ginfo(3) != 1:return	
	onclick 0

	repeat

		stick sti,256
		if sti = 256{
	
			mxy(0) = mousex/cellsize(0,listmode) ,mousey/cellsize(1,listmode)
	
			if mxy(0) = erasecell(0,listmode) & mxy(1) = erasecell(1,listmode) & cliflag = 0{
				
				cliflag = 1
				mxy_(0) = mxy(0),mxy(1)
				mindexxy(0) = (mousex-(mxy_(0)*(cellsize(0,listmode)-1))),(mousey-(mxy_(1)*(cellsize(1,listmode)-1)))
				
				gsel 4
				pos 0,0
				gcopy 1,0,0,maxscrwh(0),maxscrwh(1)
	
				gsel 3
				pos 0,0
				gzoom cellsize(0,listmode),cellsize(1,listmode),7,ecdd(0,listmode),ecdd(1,listmode),ecdd(2,listmode),ecdd(3,listmode),1

				gsel 1,0
			}
			
			if (data(mxy(0),mxy(1),listmode)) >= 10 & cliflag = 0{
				
				cliflag = 1
				mxy_(0) = mxy(0),mxy(1)
				mindexxy(0) = (mousex-(mxy_(0)*(cellsize(0,listmode)-1))),(mousey-(mxy_(1)*(cellsize(1,listmode)-1)))
				
				gsel 4
				pos 0,0
				gcopy 1,0,0,maxscrwh(0),maxscrwh(1)
	
				gsel 3
				pos 0,0
				gzoom cellsize(0,listmode),cellsize(1,listmode),data(mxy_(0),mxy_(1),listmode),dd(0,listmode),dd(1,listmode),dd(2,listmode),dd(3,listmode),1

				gsel 1,0
			}
		
		}
	
		if cliflag = 1{
			redraw 0
			pos 0,0
			gcopy 4,0,0,maxscrwh(0),maxscrwh(1)
			gmode 3,cellsize(0,listmode),cellsize(1,listmode),100
			pos mousex-mindexxy(0),mousey-mindexxy(1)
			gcopy 3,0,0,cellsize(0,listmode),cellsize(1,listmode)
			gmode 0
			redraw 1
		}
		
		if sti = 0 & cliflag = 1{
	
			if mxy_(0) = erasecell(0,listmode) & mxy_(1) = erasecell(1,listmode) {
				erasecell(0,listmode) = mxy(0)
				erasecell(1,listmode) = mxy(1)
				data(mxy(0),mxy(1),listmode) = 0
			}

			if mxy(0) = erasecell(0,listmode) & mxy(1) = erasecell(1,listmode) {
				data(mxy_(0),mxy_(1),listmode) = 0
			} else {
				temp = data(mxy_(0),mxy_(1),listmode) 
				data(mxy_(0),mxy_(1),listmode) = data(mxy(0),mxy(1),listmode)
				data(mxy(0),mxy(1),listmode) = temp
			}
	
			mxy(0) = 0,0
			mxy_(0) = 0,0
			temp = 0
			cliflag = 0
			
			gosub *draw
		}
	
		if sti = 0 & cliflag = 0{
			break
		}

		await 16

	loop
	onclick 1
	
return

//一覧作成――――――――――――――――――――――――――――――――――
*make
	
	yoko = 0
	tate = 0
	capx = 0
	capy = 0
	w = 0
	h = 0
	
	repeat cellnum(1,listmode)+1
		ycnt = cnt
		repeat cellnum(0,listmode)+1
			if data(cnt,ycnt,listmode) >= 20{
				if yoko <= cnt:yoko = cnt
				if tate <= ycnt:tate = ycnt
			}
		loop
	loop
	yoko++
	tate++
	
	if listmode = 0{
		if yubiwa = 0 & kantai = 0 : w = 382 : capx = 392
		if yubiwa = 1 & kantai = 1 : w = 440 : capx = 360
		if yubiwa = 1 & kantai = 0 : w = 408 : capx = 392
		if yubiwa = 0 & kantai = 1 : w = 415 : capx = 360
		if lvname = 1:w = 190: capx = 392:h = 279
		if page = 0:h = 279
		if page = 1:h = 315
		capy = 154
	} else {
		if sosuka {
			w = 460
			h = 365
		}
		if sosu {
			w = 230
			h = 365	
		}
		if soka {
			w = 460
			h = 195
		}
		capx = 327
		capy = 103
	}
	
	buffer 2,w*yoko,h*tate
	drawcount = 0
	repeat cellnum(1,listmode)+1
		ycnt = cnt
		repeat cellnum(0,listmode)+1
			if data(cnt,ycnt,listmode) >= 10{
				pos cnt*w,ycnt*h
				gsel data(cnt,ycnt,listmode)
				if ginfo(13) = 450:cutf = 1
				gsel 2
				gcopy data(cnt,ycnt,listmode),capx,capy-(cutf*30),w,h 
				drawcount++
				cutf = 0
			}
		loop
	loop
		
	if rengoumode = 1{
		if tuika1 {
				buffer 15,w*yoko+3,h*tate
				color 32,32,192
				boxf
				pos 0,0
				gcopy 2,0,0,w*2,h*tate
				pos w*2+3,0
				gcopy 2,w*2,0,(w*yoko)-(w*2),h*tate
				
		}
		if tuika2 {
				buffer 15,w*yoko,h*tate+3
				color 32,32,192
				boxf
				pos 0,0
				gcopy 2,0,0,w*yoko,h
				pos 0,h+3
				gcopy 2,0,h,w*yoko,h*(tate-1)
		}
	}

	savename = ""
	if drawcount != 0{
		repeat
			savename = strf("%s_%03d%s",modemes(listmode+1),cnt,savekind(jpgsave))
			if kmexist(listsavedir +"\\"+ savename) = -1{
				gdiimagesave listsavedir +"\\"+ savename,jpgquality
				break
			}
		loop
		
		gsel 0,2
			
		if kmexist(listsavedir +"\\"+ savename) <= 0{
			logmessage = savename+"の保存に失敗しました"
			objprm logid,logmessage
		} else {
			logmessage = savename+"を保存しました"
			objprm logid,logmessage
		}
	
	} else {
		gsel 0,2
		logmessage = "画像が一枚もありません"
		objprm logid,logmessage
	}

	
	dim mxy,2
	dim mxy_,2
	dim data,8,5,2
	dim bc,2
	dim dmxy,2
	bc(0) = 20,200
	
	gosub *draw
	
return

//ドロップ処理――――――――――――――――――――――――――――――――――
*OnDropFiles

	hdrop = wParam
	sdim dropfile,260
	DragQueryFile hdrop, 0, varptr(dropfile), 260
	DragFinish hdrop
	if peek(dropfile,0) = '"' : getstr dropfile, dropfile, 1, '"'
	
	if ImgF_GetFormat(dropfile) = 0{
		dropfile = 0
		gsel 1
		return
	}
	
	addfilename = dropfile
	gosub *addcaptcha
	
return

*addcaptcha
	
	if listmode = 0 {
		if (ginfo(24) = 0) | (autoaddcaptchaf = 1) {
			autoaddcaptchaf = 0
			
			dmxy(0) = cellnum(0,listmode),cellnum(1,listmode)
			
			repeat (cellnum(1,listmode)+1)*(cellnum(0,listmode)+1)
				ycnt = cnt\(cellnum(1,listmode)+1)
				xcnt = cnt/(cellnum(1,listmode)+1)
				if data(xcnt,ycnt,listmode) = 0{
					dmxy(0) = xcnt, ycnt
					break
				}
			loop
		
		} else {
			dmxy(0) = mousex/cellsize(0,listmode) , mousey/cellsize(1,listmode)
		}
	}
	
	if listmode = 1 {
		if (ginfo(24) = 0) | (autoaddcaptchaf = 1) {
			
			if tuika1 {
				repeat
					xcnt = (cnt/6)*2+cnt\2
					ycnt = (cnt\6)/2
					if data(xcnt,ycnt,listmode) = 0{
						dmxy(0) = xcnt, ycnt
						break
					}
				loop
			}
			if tuika2 {
				repeat
					xcnt = cnt\6
					ycnt = cnt/6
					if data(xcnt,ycnt,listmode) = 0{
						dmxy(0) = xcnt, ycnt
						break
					}
				loop
			}
		} else {
			dmxy(0) = mousex/cellsize(0,listmode) , mousey/cellsize(1,listmode)
		}
		
	}
	
	bc(listmode)++
	data(dmxy(0),dmxy(1),listmode) = bc(listmode)
	buffer bc(listmode)
	ImgF_PicloadEx addfilename

	gsel 1
	gosub *draw
	
	gsel 0
	logmessage = strf("%sをセル(%d,%d)に読み込みました",getpath(addfilename,11),dmxy(0),dmxy(1))
	objprm logid,logmessage
	
	gsel 1

	dmxy(0) = 0,0
	addfilename = ""
	
return

//描画――――――――――――――――――――――――――――――――――
*draw
	
	gsel 1,0
	redraw 0
	color 255,255,255
	boxf
	color 150,150,150
	
	repeat cellnum(0,listmode),1
		line  cnt*cellsize(0,listmode),-1,cnt*cellsize(0,listmode),maxscrwh(1)+1
	loop
	repeat cellnum(1,listmode),1
		line  -1,cnt*cellsize(1,listmode),maxscrwh(0)+1,cnt*cellsize(1,listmode)
	loop
	
	pos erasecell(0,listmode)*cellsize(0,listmode) ,erasecell(1,listmode)*cellsize(1,listmode)
	gzoom cellsize(0,listmode),cellsize(1,listmode),7,ecdd(0,listmode),ecdd(1,listmode),ecdd(2,listmode),ecdd(3,listmode),1

	repeat cellnum(1,listmode)+1
		ycnt = cnt
		repeat cellnum(0,listmode)+1
			if data(cnt,ycnt,listmode) >= 10{
				pos cnt*cellsize(0,listmode),ycnt*cellsize(1,listmode)
				gzoom cellsize(0,listmode),cellsize(1,listmode),data(cnt,ycnt,listmode),dd(0,listmode),dd(1,listmode),dd(2,listmode),dd(3,listmode),1
			}
		loop
	loop
	
	/*
	screen 13,cellsize(0,listmode)/2,0.18*(cellsize(0,listmode)/2)
	gmode 2,250,45
	if rengoumode {
		if tuika1 {
			repeat 12
				xcnt = (cnt/6)*2+cnt\2
				ycnt = (cnt\6)/2
				buffer 13
				gzoom cellsize(0,listmode)/2,0.18*(cellsize(0,listmode)/2),12, (cnt/6)*250 , (cnt\6)*45,250,45
				gsel 1
				gmode 2
				pos xcnt*cellsize(0,listmode)+(cellsize(0,listmode)/2),ycnt*cellsize(1,listmode)
				gcopy 13,cellsize(0,listmode)/2,0.18*(cellsize(0,listmode)/2)
				
			loop
		}
		if tuika2 {
			repeat 12
				xcnt = cnt\6
				ycnt = cnt/6
				
			loop
		}
	}
	*/
		
	
	if (mode = 2) & rengoumode{
		color 32,32,192
		if tuika1 {
			boxf  2*cellsize(0,listmode)-1, -1, 2*cellsize(0,listmode)+1, maxscrwh(1)+1
		}
		if tuika2 {
			boxf  -1, cellsize(1,listmode)-1, maxscrwh(0)+1, cellsize(1,listmode)+1
		}
		color 0,0,0
		
	}
	
	redraw 1
return

*seqcap
	
	if seqcapf = 0{
		SetTimer hwnd0,timerID,100*timersec,0
		seqcapmes = "停止"
		objprm seqcapbid,seqcapmes
		objenable comboxid,0
		objenable sscapbid,0
		objenable sscapiid,0
		objenable yabumibid,0
		seqcapf = 1
		gosub *sscaptcha
	} else {
		KillTimer hwnd0,timerID
		seqcapmes = "開始"
		objprm seqcapbid,seqcapmes
		objenable comboxid,1
		objenable sscapbid,1
		objenable sscapiid,1
		objenable yabumibid,1
		seqcapf = 0
	}
	
return

*yabumiupload
	
	if mode = 0 {	
		gosub *sscaptcha
		exec yabumidir+" \""+(sssavedir +"\\"+ savename)+"\""
	}
	if mode != 0{
		gosub *make
		if savename != ""{
			exec yabumidir+" \""+(listsavedir +"\\"+ savename)+"\""
		}
	}
return

//ホットキー――――――――――――――――――――――――――――――――――
*hotkey
	oncmd 0
	
	if (wparam = HOTKEYID) & (availablecap = 1){

		gosub *sscaptcha
		if (mode = 1 | mode = 2) {

			if autoaddcaptcha{
				addfilename = sssavedir +"\\"+ savename
				gosub *addcaptcha
			}
		}
		await 16
	}

	oncmd 1
return

*windowsize
	oncmd 0

	dupptr left,   lparam,    4
	dupptr top,    lparam+4,  4
	dupptr right,  lparam+8,  4
	dupptr bottom, lparam+12, 4

	switch wparam
	case 1
	case 2
	case 7
	case 8
		bottom = top + round(AspectRatio(listmode) * (right - left - framesx)) + framesy
		swbreak
	case 3
	case 5
	case 6
		right = left + round(1.0/AspectRatio(listmode) * (bottom - top - framesy)) + framesx
		swbreak
	case 4
		left = right - round(1.0/AspectRatio(listmode) * (bottom - top - framesy)) - framesx
		swbreak
	swend

	scrsize(0,listmode) = ginfo_winx ,round(AspectRatio(listmode)*ginfo_winx)
	cellsize(0,listmode) = round(1.0*scrsize(0,listmode) / (cellnum(0,listmode)+1)) ,round(1.0*scrsize(1,listmode) / (cellnum(1,listmode)+1))
	
	gosub *draw
	
	oncmd 1
return 1

	
//設定――――――――――――――――――――――――――――――――――
*listsavedialog
	
	BrowseFolder "一覧の保存先",(dir_exe)
	if stat = 1{
		listsavedir = refstr
		objprm listsdid,listsavedir
	}

return

*sssavedialog
	
	BrowseFolder "SSのキャプチャ保存先",(dir_exe+"\\ScreenShots")
	if stat = 1{
		sssavedir = refstr
		objprm sssdid,sssavedir
	}

return

*yabumiuploaderdialog
	
	dialog "exe",16,"YabumiUpLoader"
	if stat = 1{
		yabumidir = refstr
		objprm yabumiid,yabumidir
	}

return

*listsavefopen
	exec listsavedir,16
return

*sssavefopen
	exec sssavedir,16
return

*iniload
	
	SetIni (dir_exe+"\\makelist.ini")
	if stat = 1{
		sdim settingstr,1024
	
		settingstr = {"
						[Setting]
						yubiwa=1
						kantai=1
						page=0
						autosearch=1
						hidename=0
						namecut=0
						frontrow=0
						hidelog=0
						yabumiautoupload=0
						othergame=0
						dispcap=0
						hotkeycap=1
						autoaddcaptcha=0
						jpgsave=0
						jpgquality=90
						disableseqcap=1
						timersec=0
						listsavedir=
						sssavedir=
						yabumidir=
						hotkeystr=A___Z
						
						
						[Setting122]
						manualpos=0
						lvname=0
						rengoumode=0
						tuika1=1
						tuika2=0
						sosuka=1
						sosu=0
						soka=0
						"}

		bsave (dir_exe+"\\makelist.ini"),settingstr,strlen(settingstr)+1024
	}
	
	GetIni "Setting", "yubiwa", yubiwa
	GetIni "Setting", "kantai", kantai
	GetIni "Setting", "page", page
	GetIni "Setting", "autosearch", autosearch
	GetIni "Setting", "hidename", hidename
	GetIni "Setting", "namecut", namecut
	GetIni "Setting", "frontrow", frontrow
	GetIni "Setting", "hidelog", hidelog	
	GetIni "Setting", "yabumiautoupload", yabumiautoupload
	GetIni "Setting", "othergame", othergame
	GetIni "Setting", "dispcap", dispcap
	GetIni "Setting", "hotkeycap", hotkeycap
	GetIni "Setting", "autoaddcaptcha", autoaddcaptcha
	GetIni "Setting", "jpgsave", jpgsave
	GetIni "Setting", "jpgquality", jpgquality
	GetIni "Setting", "disableseqcap", disableseqcap
	GetIni "Setting", "timersec", timersec
	GetIni "Setting", "listsavedir", listsavedir
	GetIni "Setting", "sssavedir", sssavedir
	GetIni "Setting", "yabumidir", yabumidir
	GetIni "Setting", "hotkeystr", hotkeystr
	
	GetIni "Setting122", "lvname", lvname
	GetIni "Setting122", "rengoumode", rengoumode
	GetIni "Setting122", "tuika1", tuika1
	GetIni "Setting122", "tuika2", tuika2
	GetIni "Setting122", "sosuka", sosuka
	GetIni "Setting122", "sosu", sosu
	GetIni "Setting122", "soka", soka
	GetIni "Setting122", "manualpos", manualpos
	
	

	if (tuika1+tuika2) != 1{
		tuika1 = 1
		tuika2 = 0
	}
	
	if (sosuka+sosu+sosk) != 1{
		sosuka = 1
		sosu = 0
		soka = 0
	}
	
	if PathIsDirectory(listsavedir) = 0{
		listsavedir = dir_exe
		WriteIni "Setting", "listsavedir", listsavedir
	}
	

	if PathIsDirectory(sssavedir) = 0{
		sssavedir = dir_exe+"\\ScreenShots"
		if PathIsDirectory(sssavedir) = 0{
			chdir dir_exe
			mkdir "ScreenShots"
		}
		WriteIni "Setting", "sssavedir", sssavedir
	}

	if kmexist(yabumidir) = -1{
		yabumidir = dirinfo(0x10026) +"\\yabumi\\yabumiuploader.exe"
		availableyabumi = 1
		if kmexist(yabumidir) = -1{
			availableyabumi = 0
			yabumidir = ""
		}
	}
	WriteIni "Setting", "yabumidir", yabumidir
	
	if peek(hotkeystr,0) = 'A':hotkeycapalt=1
	if peek(hotkeystr,1) = 'C':hotkeycapctrl=1
	if peek(hotkeystr,2) = 'S':hotkeycapshift=1
	if peek(hotkeystr,3) = 'W':hotkeycapwin=1
	hotkeycapchar = strmid(hotkeystr,4,1)
	
	////////////////
	if yubiwa = 0 & kantai = 0 : w = 382 : capx = 392
	if yubiwa = 1 & kantai = 1 : w = 440 : capx = 360
	if yubiwa = 1 & kantai = 0 : w = 408 : capx = 392
	if yubiwa = 0 & kantai = 1 : w = 415 : capx = 360
	if lvname = 1:w = 190: capx = 392
	if page = 0:h = 279
	if page = 1:h = 315
	capy = 154
		
	dd(0,0) = capx,capy,w,h
	cellratio = 1.0*h/w
	cellsize(0,0) = scrsize(0,0)/(cellnum(0,0)+1),round(cellratio*cellsize(0,0))
	scrsize(1,0) = round(cellsize(1,0)* (cellnum(1,0)+1) )
	AspectRatio(0) = 1.0*scrsize(1,0)/scrsize(0,0)
	
	sosuka = 1
	tuika1 = 1
	
	if sosuka {
		w = 460
		h = 365
	}
	if sosu {
		w = 230
		h = 365	
	}
	if soka {
		w = 460
		h = 105
	}
	
	capx = 327
	capy = 103

	dd(0,1) = capx,capy,w,h
	cellratio = 1.0*h/w
	cellsize(0,1) = scrsize(0,1)/(cellnum(0,1)+1),round(cellratio*cellsize(0,1))
	scrsize(1,1) = round(cellsize(1,1)* (cellnum(1,1)+1) )
	AspectRatio(1) = 1.0*scrsize(1,1)/scrsize(0,1)
	
	
	///////////////////
	
return
		
*inisave
	
	savelogtext = ""

	if (yubiwa != 0) & (yubiwa != 1):yubiwa = 1
	WriteIni "Setting", "yubiwa",yubiwa

	if (kantai != 0) & (kantai != 1):kantai = 1
	WriteIni "Setting", "kantai",kantai
	
	if (page != 0) & (page != 1):page = 1
	WriteIni "Setting", "page",page

	if (autosearch != 0) & (autosearch != 1):autosearch = 1
	WriteIni "Setting", "autosearch",autosearch

	if (hidename != 0) & (hidename != 1):hidename = 0
	WriteIni "Setting", "hidename",hidename

	if (namecut != 0) & (namecut != 1):namecut=0
	WriteIni "Setting", "namecut",namecut
	
	if (frontrow != 0) & (frontrow != 1):frontrow=1
	WriteIni "Setting", "frontrow",frontrow
	
	if (hidelog != 0) & (hidelog != 1):hidelog = 0
	WriteIni "Setting", "hidelog",hidelog

	if (yabumiautoupload != 0) & (yabumiautoupload != 1):yabumiautoupload=1
	WriteIni "Setting", "yabumiautoupload",yabumiautoupload
	
	if (othergame != 0) & (othergame != 1):othergame = 0
	WriteIni "Setting", "othergame",othergame
	
	if (dispcap != 0) & (dispcap != 1):dispcap=1
	WriteIni "Setting", "dispcap",dispcap

	if (hotkeycap != 0) & (hotkeycap != 1): hotkeycap = 1
	WriteIni "Setting", "hotkeycap",hotkeycap

	if (autoaddcaptcha != 0) & (autoaddcaptcha != 1):autoaddcaptcha=1
	WriteIni "Setting", "autoaddcaptcha",autoaddcaptcha

	if (jpgsave != 0) & (jpgsave != 1):jpgsave=0
	WriteIni "Setting", "jpgsave",jpgsave
	
	if (jpgquality < 1) | (jpgquality > 100):jpgquality = 90
	WriteIni "Setting", "jpgquality",jpgquality
	
	if (disableseqcap != 0) & (disableseqcap != 1):disableseqcap = 1
	WriteIni "Setting", "disableseqcap",disableseqcap

	if (timersec < 0) | (timersec > 999):timersec = 5
	WriteIni "Setting", "timersec",timersec

	if PathIsDirectory(listsavedir) {
		WriteIni "Setting", "listsavedir", listsavedir
	} else {
		savelogtext += "Info - 一覧保存先が存在しないためデフォルトに変更しました\n"
		listsavedir = dir_exe
		WriteIni "Setting", "listsavedir", listsavedir
		objprm listsdid,listsavedir
	}
	
	if PathIsDirectory(sssavedir) {
		WriteIni "Setting", "sssavedir",  sssavedir
	} else {
		savelogtext += "Info - SS保存先が存在しないためデフォルトに変更しました\n"
		chdir dir_exe
		if PathIsDirectory(dir_exe+"\\ScreenShots") = 0 {
			mkdir "ScreenShots"
		}
		sssavedir = dir_exe+"\\ScreenShots"
		WriteIni "Setting", "sssavedir", sssavedir
		objprm sssdid,sssavedir
	}

	if kmexist(yabumidir) = -1{
		yabumidir = dirinfo(0x10026) +"\\yabumi\\yabumiuploader.exe"
		if kmexist(yabumidir) = -1{
			availableyabumi = 0
			yabumidir = ""
			WriteIni "Setting", "yabumidir", yabumidir
		} else {
			availableyabumi = 1
			WriteIni "Setting", "yabumidir", yabumidir
		}
	} else {
		availableyabumi = 1
		WriteIni "Setting", "yabumidir", yabumidir
	}
		
	hotkeystr = ""
		
	if (hotkeycapalt + hotkeycapctrl + hotkeycapshift + hotkeycapwin) = 0{
		savelogtext += "Info - ホットキーの設定が不適切です(1) 設定をデフォルトに変更しました\n"
		hotkeycap = 0
		hotkeycapalt = 1
		hotkeycapctrl = 0
		hotkeycapshift= 0
		hotkeycapwin = 0
	}
	
	if hotkeycapalt	= 1	:poke hotkeystr,0,'A':else:poke hotkeystr,0,95
	if hotkeycapctrl = 1:poke hotkeystr,1,'C':else:poke hotkeystr,1,95
	if hotkeycapshift= 1:poke hotkeystr,2,'S':else:poke hotkeystr,2,95
	if hotkeycapwin	= 1	:poke hotkeystr,3,'W':else:poke hotkeystr,3,95
	
	if (65 <= peek(hotkeycapchar)) & (peek(hotkeycapchar) <= 90){
		hotkeycode = peek(hotkeycapchar)
		poke hotkeystr,4,hotkeycode
	} else {
		if (97 <= peek(hotkeycapchar)) & (peek(hotkeycapchar) <= 122){
			hotkeycode = peek(hotkeycapchar)-32
			hotkeycapchar = strf("%c",hotkeycode)
			poke hotkeystr,4,hotkeycode
		} else {
			savelogtext += "Info - ホットキーの設定が不適切です(2) 設定をデフォルトに変更しました\n"
			hotkeycode = 90
			hotkeycapchar = strf("%c",hotkeycode)
			poke hotkeystr,4,hotkeycode
			hotkeycap = 0
		}
	}
	
	WriteIni "Setting", "hotkeystr", hotkeystr
	
	if (manualpos != 0) & (manualpos != 1):manualpos=0
	WriteIni "Setting122", "manualpos",manualpos
	
	if (lvname != 0) & (lvname != 1):lvname=0
	WriteIni "Setting122", "lvname",lvname
	
	if (rengoumode != 0) & (rengoumode != 1):rengoumode=0
	WriteIni "Setting122", "rengoumode",rengoumode
	
	if (tuika1 != 0) & (tuika1 != 1):tuika1=1
	WriteIni "Setting122", "tuika1",tuika1
	if (tuika2 != 0) & (tuika2 != 1):tuika2=0
	WriteIni "Setting122", "tuika2",tuika2
	
	if (tuika1+tuika2) != 1{
		tuika1 = 1
		tuika2 = 0
	}
	
	if (sosuka != 0) & (sosuka != 1):sosuka=1
	WriteIni "Setting122", "sosuka",sosuka
	if (sosu != 0) & (sosu != 1):sosu=0
	WriteIni "Setting122", "sosu",sosu
	if (soka != 0) & (soka != 1):soka=0
	WriteIni "Setting122", "soka",soka
	
	if (sosuka+sosu+sosk) != 1{
		sosuka = 1
		sosu = 0
		soka = 0
	}
	
	if savelogtext != ""{
		savelogtext += "makelist.iniを保存しました"
		dialog savelogtext
	}
	

	
	/*/////////////////////////////////////////
	
	lvname
	rengoumode
	tuika1
	tuika2
	sosuka
	sosu
	soka
	
	*//////////////////////////////////////////


return

*hotkeycapsetting

	if (hotkeycap = 1) & (hotkeystr != hotkeystr_temp) {
		UnregisterHotKey hwnd0,HOTKEYID
		RegisterHotKey hwnd0,HOTKEYID,(MOD_ALT*hotkeycapalt)|(MOD_CONTROL*hotkeycapctrl)|(MOD_SHIFT*hotkeycapshift)|(MOD_WIN*hotkeycapwin),hotkeycode
		hotkeystr_temp = hotkeystr
		if stat = 0{
			logmessage = "ホットキーの設定に失敗しました"
			objprm logid,logmessage
			hotkeyset = 0
		} else {
			logmessage = "ホットキーを設定しました"
			objprm logid,logmessage
			hotkeyset = 1
		}
	}
	
	if (hotkeycap = 0) & (hotkeyset = 1){
		UnregisterHotKey hwnd0,HOTKEYID
		logmessage = "ホットキーを解除しました"
		objprm logid,logmessage
		hotkeyset = 0
		hotkeystr_temp = ""
	}

	if hotkeyset = 1 & availablecap = 1 {
		gsel 0,0
		title "*メイン"
	} else {
		gsel 0,0
		title "メイン"
	}
	
return


*disinfoinit
	
	disinfo(0) = GetSystemMetrics(76)
	disinfo(1) = GetSystemMetrics(77)
	disinfo(2) = GetSystemMetrics(78)
	disinfo(3) = GetSystemMetrics(79)

	init_makelist disinfo

	hdcScreen = CreateDC("DISPLAY", NULL, NULL, NULL)
	
return


*en_d
	
	if wparam = 1{
		gosub *ssmode
		objprm comboxid,0
		return
	}

	DeleteDC hdcScreen
	
	DragAcceptFiles hwnd1, 0
	DragAcceptFiles hwnd0, 0
	
	if seqcapf = 1{
		KillTimer hwnd0,timerID
	}
	gosub *inisave
	if hotkeyset = 1 {
		UnregisterHotKey hwnd0,HOTKEYID
	}
	end